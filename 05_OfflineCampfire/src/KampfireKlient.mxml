<?xml version="1.0" encoding="utf-8"?>
<!-- 
	Demonstrates System Integration
	Dock/Systray icon manipulation
	Native Menues
	Custom Chrome Windows
	Network Events
	 
-->
<mx:WindowedApplication
	xmlns:mx="http://www.adobe.com/2006/mxml"
	xmlns:local="*"
	layout="absolute"
	width="100%"
	height="100%"
	applicationComplete="setup()"
>

<mx:Script>
	<![CDATA[
		import mx.managers.PopUpManager;
		import mx.utils.StringUtil;
		import flash.utils.clearInterval;
		import flash.utils.setInterval;
		import flash.desktop.NativeApplication;
	
		[Bindable]
		private var account:String=null;
	
		private var lastMessageId:String="not-a-message";
		private var intervalId:Number=-1;
		private var restoreOnMessage:Boolean;
		private var growlOnMessage:Boolean;
		private var showingPreferences:Boolean;
		
		private var preferencesView:SettingsView;
		
		/**
		 * Called after application initialization
		 * 
		 */
		private function setup():void {
			readPreferences();
			configureMenus();
			configureListeners();
			ping();
			accountField.setFocus();
		}
		
		/**
		 * Replace system menus with an 'Edit' Menu
		 * that triggers the settings view 
		 */
		private function configureMenus():void {
			var rootMenu:NativeMenu = new NativeMenu();
			var editMenu:NativeMenu = new NativeMenu();
			var settingsMenuItem:NativeMenuItem = new NativeMenuItem("Preferences");
			settingsMenuItem.addEventListener(Event.SELECT, settingsMenuSelected);

			rootMenu.addSubmenu(editMenu, "Edit");
			editMenu.addItem(settingsMenuItem);
			
			// decide where the "Edit" menu goes
			if(NativeApplication.supportsMenu) {
				NativeApplication.nativeApplication.menu = rootMenu;
				DockIcon(NativeApplication.nativeApplication.icon).menu = rootMenu;
			} else if(NativeWindow.supportsMenu) { 
				nativeWindow.menu = rootMenu;
				SystemTrayIcon(NativeApplication.nativeApplication.icon).menu = rootMenu;
			}

		}
		
		/**
		 * Listen for two events:
		 * <li />When a network change event occurs, call checkNetwork
		 * <li />When window recieves focus, call windowActivate on the unreadMessageManager
		 */ 
		private function configureListeners():void {
			NativeApplication.nativeApplication.addEventListener(Event.NETWORK_CHANGE, checkNetwork);
			NativeApplication.nativeApplication.addEventListener(Event.ACTIVATE, unreadMessageManager.windowActivate);
		}
		
		/**
		 * Starts periodically checking for new messages
		 */
		private function checkForNewMessages():void {
			// setInterval periodically calls a given method
			if (intervalId==-1)
				intervalId = setInterval(checkMessages, 5*1000);			
		}
		
		/**
		 * Stops periodically checking for new messages
		 */
		private function stopCheckingMessages():void {
			if (intervalId!=-1) {
			   clearInterval(intervalId)
			   intervalId = -1;
			}
		}
		
		private function accountSelected():void {
			account = accountField.text;
			currentState='accountSelected';
		}
		
		/**
		 * Checks all messages in the table of messages in the HTML control
		 * If we haven't seen the message ids listed there before, we have new messages
		 */
		//FIXME: see if we can set an html event to trap when the body of a table changes.	
		private function checkMessages():void {
			if (currentState!='accountSelected') return;
			try { // We may not be in a campfire room.			
			  var messages:Array = html.domWindow.$$('tbody#chat > tr');
			} catch (err:Error) {
			  return;
			}
			
			var currentMessage:Object = messages!= null && messages.length>0 ? messages[messages.length-1] : null;
			var currentMessageId:String = currentMessage ? currentMessage.id : 'no message' 
			if (lastMessageId != currentMessageId) {
				try { // in case we don't recognize the html
					var xml:XML = new XML(currentMessage.outerHTML as String);
					var person:String = xml..td.(@['class'] == 'person').span.text();
					var message:String = xml..td.(@['class'] == 'body').div.text();
					lastMessageId = currentMessageId;
					if (!StringUtil.isWhitespace(person)) // no notification if the "person" is campfire
						dispatchNewMessageDetected(person, message);
				} catch (e:Error) {
					return;
				}
			}		
		}
		
		/**
		 * New message recieved
		 */
		private function dispatchNewMessageDetected(person:String, message:String):void {
			// if we're not the active window
			if (!NativeApplication.nativeApplication.activeWindow) {
				// bounce the dock icon if there is one
				if (NativeApplication.supportsDockIcon){		
					var dockicon:DockIcon = NativeApplication.nativeApplication.icon as DockIcon;
					dockicon.bounce("informational"); //Note, only bounces if application is not active.
				}
				
				// tell the unread message manager we have new messages
				unreadMessageManager.newUnreadMessage();
				restoreIfMinimized();
				growl(person, message);
			}
		}
		
		/**
		 * If settings allow, shows a growl type notification of a new message
		 */
		private function growl(person:String, message:String):void {
			if (growlOnMessage) {
				var growlWindow:MessageNotification = new MessageNotification();
				growlWindow.showMessage(person, message);
			}
		}
		
		/**
		 * If settings allow, restore window from minimized state
		 */
		private function restoreIfMinimized():void {
			if (nativeWindow.displayState == NativeWindowDisplayState.MINIMIZED && restoreOnMessage) {
				restore();
			}
		}
		
//========================================================================
//  Preferences
//========================================================================
		
		private function settingsMenuSelected(e:Event):void {
			showPreferencesEdtor();
		}
		
		private function showPreferencesEdtor():void {
			if (!showingPreferences) {
				if (preferencesView == null) {
					preferencesView = PopUpManager.createPopUp(this, SettingsView, true) as SettingsView;
					preferencesView.addEventListener('close', hidePreferencesEditor);
					PopUpManager.centerPopUp(preferencesView);
				} else {
					PopUpManager.addPopUp(preferencesView, this, true);
				}
				showingPreferences = true;
			}
		}
		
		private function hidePreferencesEditor(e:Event=null):void {
			PopUpManager.removePopUp(preferencesView);
			readPreferences();
			showingPreferences = false;
		}
		
		private function readPreferences():void {
			var so:SharedObject = SharedObject.getLocal('air-campfire-prefs');
			if (so.data['restoreOnMessage'] != null || so.data['growlOnMessage'] != null) {
				restoreOnMessage = so.data['restoreOnMessage'] as Boolean;
				growlOnMessage = so.data['growlOnMessage'] as Boolean;
			}
		}
		
//========================================================================
//  Network
//========================================================================

		
		[Bindable]
		private var connected:Boolean=true;
		
		private function checkNetwork(event:Event):void {
			ping();
		}
		
		private function ping():void{
		    var headRequest:URLRequest = new URLRequest();
		    headRequest.method = "HEAD";
		    headRequest.url = "http://www.campfirenow.com/";
		    logEvent("Requesting: " + headRequest.url.toString());		
		    var response:URLLoader = new URLLoader(headRequest);
		    //response.addEventListener(HTTPStatusEvent.HTTP_STATUS,onHTTPStatus);
		    response.addEventListener(Event.COMPLETE,onComplete);
	        response.addEventListener(IOErrorEvent.IO_ERROR,onError);  
	        //response.addEventListener(SecurityErrorEvent.SECURITY_ERROR,onSecurityError);
		}
				
		private function onComplete(status:Event):void{
		    logEvent("    Response received.");
		    connected = true;
		}
				
		private function onError(error:IOErrorEvent):void{
		    logEvent("    Request failed. " + error.text);
		    connected = false;
		}
							
		private function logEvent(entry:String):void {
		    trace(entry);
		}
		
	]]>
</mx:Script>

	<mx:states>
		<mx:State name="accountSelected">
			<mx:RemoveChild target="{accountSelectionBox}"/>
			<mx:AddChild relativeTo="{html}" position="before">
				<mx:HBox id="menuContainer" width="100%" horizontalAlign="right">
					<mx:LinkButton label="Settings" click="showPreferencesEdtor()"/>
					<mx:LinkButton label="Select Account" click="currentState=''"/>
				</mx:HBox>
			</mx:AddChild>
			<mx:SetProperty target="{html}" name="location" value="http://{account}.campfirenow.com"/>
		</mx:State>
	</mx:states>

	
	<mx:Label text="[NOT CONNECTED]" visible="{!connected}" includeInLayout="{!connected}" fontSize="18" color="#FD021A"/>
	<mx:HBox width="100%" id="accountSelectionBox" paddingTop="5" paddingBottom="5" paddingLeft="5" paddingRight="5" verticalAlign="middle">
		<mx:TextInput id="accountField" enter="accountSelected()" />
		<mx:Button label="Select>" click="accountSelected()" />
		<mx:Label text=" or " />
		<mx:LinkButton label="Cancel" click="currentState='accountSelected'" enabled="{account!=null}" />
	</mx:HBox>
	
	<local:UnreadMessageManager id="unreadMessageManager" top="{accountSelectionBox.height}" />
	
	<!-- FIXME: create a html page that can be access/distributed from the application -->
	<mx:HTML top="{accountSelectionBox.height}" width="100%" height="100%" id="html"
		location="app:/pleaseWait.html" 
		locationChange="trace('locationChange');stopCheckingMessages();" 
		htmlDOMInitialize="trace('htmlDOMInitialize'); " 
		complete="trace('complete'); checkForNewMessages();" 
		initialize="trace('initialize')" />
		
</mx:WindowedApplication>
