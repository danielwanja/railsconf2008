<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" width="400" height="300">
<mx:Metadata>
	[Event(name="loaded")]
</mx:Metadata>
<mx:Script>
	<![CDATA[
		import mx.events.CloseEvent;
		import data.HTMLParser;
		import data.TweetDataAggregator;
		
		import mx.rpc.AsyncToken;
		import mx.collections.ArrayCollection;
		import mx.controls.Alert;
		import mx.utils.ObjectUtil;
		
		import mx.rpc.events.FaultEvent;
		import mx.rpc.events.ResultEvent;

		private var cancel:Boolean=false;
	
		[Bindable]
		private var user:String;

		[Bindable]
		private var tweetsToLoad:Number=0;

	
		[Bindable]
		public var dataAggregator:TweetDataAggregator;
		
		public function load(aggregator:TweetDataAggregator):void {
			this.user = aggregator.accountName;
			cancel = false;
			content.text = "Loading "+user+" ...";
			dataAggregator = aggregator;
			tweetsToLoad = 0;
			progress.setProgress(0,100);
			twitter.send();
trace("Starting");			
		}
		private function loadPage(page:Number): void {
			content.text = "Loading "+user+" page "+page+" ...";
			var call:AsyncToken = twitter.send({page:page});
			call.pageNumber = page;
trace("Page:"+page);			
		}
		private function pageLoaded(event:ResultEvent):void {
trace("Page loaded:"+event.token.pageNumber);			
			var stringHTML:String = event.result as String;
			var parser:HTMLParser = new HTMLParser(stringHTML, event.token.pageNumber);
			content.text =  "Updates:"+ parser.updates;
			content.text += "\nPages:"+ parser.pageCount;
			if (tweetsToLoad==0) {
				tweetsToLoad = parser.updates;//FIXME: support both load and refresh i.e.- dataAggregator.getTweets().length;
			}
			var overlap:Boolean = dataAggregator.addTweets(parser.tweetsArray);
			progress.setProgress(dataAggregator.getTweets().length, tweetsToLoad);
			if (!overlap &&!cancel&&parser.hasNextPage) {
				loadPage(parser.pageNumber+1);
			} else {
				trace("Overlap:"+overlap);
				loadCompleted();
			}	
		}
		private function loadCompleted():void {
trace("Done")			
			dataAggregator.aggregate();
			FileDB.save(dataAggregator);
			dispatchEvent(new Event("loaded"));
		}
		
		private var currentPage:Number;
		private function pageDidntLoad(event:FaultEvent):void {
trace("Page failed:"+event.token.pageNumber);						
			currentPage = event.token.pageNumber;
trace("Error detail\t:"+event.fault.faultDetail);
trace("Error root cause\t:"+event.fault.rootCause);
trace("Error name\t:"+event.fault.name);
trace("Error fault string\t:"+event.fault.faultString);
trace("Error fault code\t:"+event.fault.faultCode);

			Alert.show(event.fault.message, "Issue accessing Twitter. Retry?", 
						Alert.YES|Alert.NO, null, retryLoadAnswer, null, Alert.YES ); 
		}		
		
		private function retryLoadAnswer(eventObj:CloseEvent):void {
            if (eventObj.detail==Alert.YES) {
               loadPage(currentPage);
            } else {
				// Let's save what is loaded so far
				dataAggregator.aggregate();
				FileDB.save(dataAggregator);            	
            }
		}
	]]>
</mx:Script>
<mx:HTTPService id="twitter"  
				url="http://twitter.com/{user}"
				resultFormat="text" 
				result="pageLoaded(event)" fault="pageDidntLoad(event)"	/>
	
<mx:HBox>
	<mx:Button label="Cancel" click="cancel=true" />
</mx:HBox>
<mx:ProgressBar id="progress"  mode="manual" label="Loading {tweetsToLoad} tweets: %3%% for {user} "/>
<mx:TextArea id="content" width="100%" height="100%" />		
</mx:VBox>
